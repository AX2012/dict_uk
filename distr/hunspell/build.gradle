apply plugin: 'java'
apply plugin: 'groovy'


String outDir = "build/hunspell"
version=new File("../../VERSION").text.trim()

println "Version: $version"

repositories {
    mavenCentral()
}


dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.5'

    compile group: 'org.codehaus.gpars', name: 'gpars', version: '1.2.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}


sourceSets {
  main {
    java {
        srcDir '../../src/main/java'
    }
    groovy {
        srcDir 'src/main/groovy'
        srcDir '../../src/main/groovy'
    }
    resources {
        srcDir '../../src/main/resources'
    }
  }
}

compileGroovy {
    exclude '**/tools/other/*.groovy'
}


task hunspell (type: JavaExec, dependsOn: classes) {
    outputs.files "$outDir/uk_UA.aff", "$outDir/uk_UA.dic"

    doFirst {
        new File(outDir).mkdirs()
    }

    classpath = sourceSets.main.runtimeClasspath
    main = "Hunspell"
}

task hunspellZip (type: Zip, dependsOn: hunspell) {
   from "$outDir"
   include 'uk_UA.aff'
   include 'uk_UA.dic'
   archiveName "hunspell-uk_UA_${version}.zip"
   destinationDir(file("$outDir"))
}

task hunspellForSearch (type: JavaExec, dependsOn: classes) {
    outputs.files "$outDir/uk_UA.aff", "$outDir/uk_UA.dic"

    doFirst {
        new File(outDir).mkdirs()
    }

    classpath = sourceSets.main.runtimeClasspath
    main = "Hunspell"
    args "-forSearch"
}




task testHunspell (type: Exec) { //, dependsOn: hunspell) {
    def txt = "головатого білого білолицьому ницого"
    txt += " швидко вже"
    txt += " хата хатами дами вимені воріт Карпат кайданів вовчища селом сіл агрошкіл автовізка"
    txt += " карбованця авантюрнице авантюрницею"
    txt += " абонував абортує гуляв хропу сядете живемо абонуватиму жнеш вечоріло"
    txt += " абонувавши абонуючи хроплячи"
    txt += " заправлено казано вимито метено введено"
    txt += " обікрав вжену уженіться"
    txt += " Білгород-Дністровського Захер-Мазоха Захера-Мазоха"
    txt += " відіб\\'ю"
    txt += " білолицого пілі відб\\'ю"
    def cmd = "echo $txt | hunspell -d $outDir/uk_UA"

    commandLine "sh", "-c", "${cmd}"
}

task testHunspellDirect (type: Exec) { //, dependsOn: hunspell) {
    def cmd = "hunspell -d $outDir/uk_UA -l ../../out/words_spell.txt > hunspell.unknown.txt; wc -l hunspell.unknown.txt"

    commandLine "sh", "-c", "${cmd}"
}
